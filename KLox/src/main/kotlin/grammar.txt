program        → statement* EOF ;

statement      → exprStmt
               | printStmt ;

exprStmt       → expression ";" ;
printStmt      → "print" expression ";" ;

expression     → equality ;
equality       → comparison ( ( "!=" | "==" ) comparison )* ;
comparison     → term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
term           → factor ( ( "-" | "+" ) factor )* ;
factor         → unary ( ( "/" | "*" ) unary )* ;
unary          → ( "!" | "-" ) unary
               | primary ;
primary        → NUMBER | STRING | "true" | "false" | "nil"
               | "(" expression ")" ;


++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++


Grammar notation        Code representation
---------------------------------------------------------
Terminal	            Code to match and consume a token
Non-terminal	        Call to that rule’s function
|	                    If or switch statement
* or +	                While or for loop
?	                    If statement
